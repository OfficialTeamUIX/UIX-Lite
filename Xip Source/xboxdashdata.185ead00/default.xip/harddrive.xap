var bLauncherVisible0;
var bInRecentTitles;
var bRecentToggled;
var nPlayCursor0;
var nPlayCursor1;
var nPreviousPlayCursor0;
var nPlayScroll0;
var nPlayScroll1;
var nPlayVisible0;
var nCurLauncherItem0;
var nCurLauncherScroll0;
var nCurLauncherVisible0;
var subTitle0;
var subTitle1;
var CurrentSection;
var TempTitleList;
var CurFillDir;
var MaxRecentTitles;
var arrRecentNames;
var arrRecentPaths;
var arrMenuNames;
var arrMenuPaths;
var TotalSections;
var LastTitleLaunched;
var DefaultIcon;

function initialize()
{
	bLauncherVisible0 = true;
	bInRecentTitles = false;
	bRecentToggled = false;
	nCurLauncherItem0 = 0;
	nCurLauncherScroll0 = 0;
	nPlayCursor0 = 0;
	nPreviousPlayCursor0 = 0;
	nPlayScroll0 = 0;
	nPlayVisible0 = 9;
}

function UpdateTrackList()
{
	var c = theLauncher.children[0].children[0];
	var nTracks0 = TempTitleList.length;
	c.LowerPanelText.text = subTitle0;
	
	if (nPlayCursor0 < 0 | nPlayScroll0 < 0)
	{
		nPlayScroll0 = 0;
		nPlayCursor0 = 0;
	}
	else if ( (nPlayCursor0 >= (nPlayVisible0 - 1)) & (nPreviousPlayCursor0 < nPlayCursor0) & (nPlayCursor0 > (nPlayScroll0 + (nPlayVisible0 - 1)) ))
	{    
		nPlayScroll0 = nPlayCursor0 - (nPlayVisible0 - 1);
	}
	else if (nPlayCursor0 < nPlayScroll0)
	{
		nPlayScroll0 = nPlayCursor0;
	}
	nPreviousPlayCursor0 = nPlayCursor0;
	
	if (nPlayScroll0 > 0)
	{
		c.MU_morearrow_up.visible = true;
	}
	else
	{
		c.MU_morearrow_up.visible = false;
	}
	
	if ((nPlayScroll0 + nPlayVisible0) < nTracks0)
	{
		c.MU_morearrow_down.visible = true;
	}
	else
	{    
		c.MU_morearrow_down.visible = false;
	}
	
	for (var i = 0; i < nPlayVisible0; i = i + 1)
	{
		var n = nPlayScroll0 + i;
		if (n >= nTracks0)
		{
			c.TrackNames.children[i].children[0].geometry.text = "";
			c.TrackNumbers.children[i].children[0].geometry.text = "";
		}
		else
		{
			var strName0, strTime0;
			strName0 = TempTitleList[n];
			var strAltName = GetSavedValue("AltTitleName",strName0);
			if (strAltName != "") strName0 = strAltName;
			if(strName0.length() > 36) strName0 = strName0.substr(0,36) + "...";
			strTime0 = "";
			
			c.TrackNames.children[i].children[0].geometry.text = strName0;
			c.TrackNumbers.children[i].children[0].geometry.text = strTime0;

			if (n == nPlayCursor0)
			{
				c.TrackNames.children[i].SetScale(0.14, 0.14, 0.14);
				c.TrackNames.children[i].children[0].appearance.name = "CellEgg/Partsz";
				c.TrackNumbers.children[i].children[0].appearance.name = "CellEgg/Partsz";
				var Icon = IconFile.GetValue(TempTitleList[nPlayCursor0]);
				if(Icon == "Unknown" | Icon == "") Icon = DefaultIcon; else  Icon = Icon + ".xbx";
				c.theIcons.children[0].children[0].appearance.texture.url = Icon; 
			}
			else
			{               
				c.TrackNames.children[i].SetScale(0.14, 0.14, 0.14);
				c.TrackNames.children[i].children[0].appearance.name = "Material #1335";
				c.TrackNumbers.children[i].children[0].appearance.name = "Material #1335";
			}
		}
	}
	 
	if (nTracks0 == 1)
	{    
		c.SubMenuHeaderText.text = nTracks0 + " " + Translate("Title");
	}
	else
	{
		c.SubMenuHeaderText.text = nTracks0 + " " + Translate("Titles");
	}
}

DEF theLauncherAlternateViewpoint Viewpoint
{
	fieldOfView 1.387000
	orientation 1.177400 -0.983500 -0.236250 -0.345440
	position -47.889999 54.662001 -199.299994
	jump false
}

DEF LauncherViewpoint Viewpoint
{
	fieldOfView 1.287000
	orientation 0.200900 0.979000 0.034330 -0.345700
	position -47.889999 54.662001 -202.299994
	jump false
}

DEF theTitleListViewpoint Viewpoint
{
	fieldOfView 1.316000
	orientation -0.017440 -0.999800 0.000769 -0.088190
	position -45.230000 54.810001 -202.299994
	jump false
}

function ShowLauncherMenu()
{
	var c = theLauncher.children[0].children[0];
	c.MU_L3_text_tracklist01.visible = false;
	c.theMenuArm.SetRotation(0, 1, 0, 0);
	c.audio_panel.SetRotation(-1, 0, 0, -3.246000);
	c.audio_panel.SetTranslation(-45.549999, 57.18, -203.800003);
	c.back1.SetTranslation(-48.990000, 52.450001, -205.000000); 
	c.back1.SetRotation(0.000000, 1.000000, 0.000000, -0.130900);
	c.select1.SetTranslation(-45.280000, 52.339999, -204.000000);   
	c.select1.SetRotation(0.000000, 1.000000, 0.000000, -0.497400);
	UpdateLauncherMenu();
	LauncherViewpoint.isBound = true;
	bLauncherVisible0 = true;
	bInRecentTitles = false;
	bRecentToggled = false;
	if(c.theIcons.visible) c.theIcons.children[0].children[0].appearance.texture.url = DefaultIcon;
}

function StartTitleList()
{
	var c = theLauncher.children[0].children[0];
	c.MU_L3_text_tracklist01.visible = false;
	c.back1.SetTranslation(-47.340000, 52.809999, -204.300003); 
	c.back1.SetRotation(0.000000, -1.000000, 0.000000, -0.340300);
	c.select1.SetTranslation(-43.539999, 52.740002, -204.800003);   
	c.select1.SetRotation(-0.014820, 0.970100, -0.242300, -0.143900);
	c.theMenuArm.SetRotation(0, 1, 0, 2);
	c.audio_panel.SetRotation(1, 0, 0, -0.02618);
	c.audio_panel.SetTranslation(-45.549999, 55.07, -204.5);
	nPlayScroll0 = 0;
	nPlayCursor0 = 0;
	UpdateTrackList();
	theTitleListViewpoint.isBound = true;
	CurrentViewpoint = theTitleListViewpoint;
	bLauncherVisible0 = false;
}

function EnterLauncherMenu()
{
	var c = theLauncher.children[0].children[0];
	c.theIcons.visible = true;
	var Icon = GetSavedValue("default","LauncherOrbIcon");
	if (Icon == "Xbox") { DefaultIcon = "xboxlogo.bmp"; }
	else if (Icon == "UIX") { DefaultIcon =  "uixlogo.xbx"; }
	else { DefaultIcon =  ""; c.theIcons.visible = false; }
	nPlayScroll0 = 0;
	nCurLauncherScroll0 = 0;
	nCurLauncherItem0 = 0;
	c.TitleText1.text = theTranslator.Translate("Xbox Hard Disk");      //menu title
	LoadMenuItems();
	ShowLauncherMenu();
	CurrentViewpoint = LauncherViewpoint;
	CurrentAltViewpoint = theLauncherAlternateViewpoint;
	LoadRecentTitles();
}

function LoadMenuItems()
{
	var c = theLauncher.children[0].children[0];
	TotalSections = 0;
	arrMenuNames = new Array;
	arrMenuPaths = new Array;
	for (var i = 0; i < 8; i = i + 1)
	{
		if(i < 5) c.theMenuButtons.children[i].visible = false;
		var v = GetSavedValue("LauncherMenu", "Title" + i);
		if(v != "") 
		{
			arrMenuNames[TotalSections] = v;
			arrMenuPaths[TotalSections] = GetSavedValue("LauncherMenu", "Path" + i);
			TotalSections = TotalSections + 1;
		}
	}
	if(TotalSections >= 3) { nCurLauncherVisible0 = Math.min(5, TotalSections); } else { nCurLauncherVisible0 = 2; }
}

function LoadRecentTitles()
{
	arrRecentNames = new Array;
	arrRecentPaths = new Array;
	MaxRecentTitles = 9;
	for (var i = 0; i < MaxRecentTitles; i = i + 1)
	{
		var v = GetSavedValue("RecentTitles", "Title" + i);
		if(v != "") 
		{
			var arrTemp = Split(v,"|");
			arrRecentPaths[i] = arrTemp[0];
			arrRecentNames[i] = arrTemp[1];
		}
	}
}

function UpdateRecentTitles(x,y)
{
	SetSavedValue("RecentTitles", "Title0", x + "|" + y );
	var index = 1;
	for (var i = 0; i < arrRecentNames.length; i = i + 1)
	{
		if(arrRecentNames[i] != y)
		{
			SetSavedValue("RecentTitles", "Title" + index, arrRecentPaths[i] + "|" + arrRecentNames[i] );
			index = index + 1;
			if(index == MaxRecentTitles) return;
		}
	}
}

//sets up the root menu text etc.
function UpdateLauncherMenu()
{
	var c = theLauncher.children[0].children[0];
	
	if (nCurLauncherScroll0 > 0)
	{
		c.morearrow_up.visible = true;
		c.morearrow_up.children[0].appearance.material.name = "NavType";
	}
	else
	{
		c.morearrow_up.visible = false;
	}
	
	if ((nCurLauncherScroll0 + nCurLauncherVisible0) < TotalSections)
	{
		c.morearrow_down.visible = true;
		c.morearrow_down.children[0].appearance.material.name = "NavType";
	}
	else
	{    
		c.morearrow_down.visible = false;
	}
	
	for (var i = 0; i < nCurLauncherVisible0; i = i + 1)
	{
		var n = nCurLauncherScroll0 + i;
		var v = arrMenuNames[n];
		c.theMenuButtons.children[i].visible = true;
		c.theMenuButtons.children[i].children[1].children[0].geometry.text = v;
		
		if (n == nCurLauncherItem0)
		{
			c.theMenuButtons.children[i].children[0].appearance.material.name = "GameHilite";
			c.theMenuButtons.children[i].children[0].appearance.texture.url = "GameHilite_01.bmp";
			c.theMenuButtons.children[i].children[1].children[0].appearance.name = "HilightedType";
			c.MenuMetaText.text = v;
			if(i == 0) c.morearrow_up.children[0].appearance.material.name = "HilightedType";
			else if(i == 4) c.morearrow_down.children[0].appearance.material.name = "HilightedType";
		}
		else
		{
			c.theMenuButtons.children[i].children[0].appearance.material.name = "GameHilite";
			c.theMenuButtons.children[i].children[0].appearance.texture.url = "outline.tga";
			c.theMenuButtons.children[i].children[1].children[0].appearance.name = "NavType";
		}
	}
	c._button_surround.visible = (TotalSections >= 3);
	c.New_Line11.visible = (TotalSections >= 3);
	c._button_surround_0.visible = (TotalSections < 3);
}

function LaunchItem(x,y)
{
	var RelativePath = Split(x,";");
	var FolderName = y;
	
	for(var i = 0; i < PartitionLocations.length; i = i + 1)
	{
		for(var ix = 0; ix < RelativePath.length; ix = ix + 1)
		{
			launchPath = PartitionLocations[i] + RelativePath[ix] + "\\" + FolderName;
			launchXbe = "default.xbe";
			if(i > 1 & bHDDInitialized == false)
			{
				sLocation = x;
				sFolder = y;
				bLaunchItem = true;
				bHDDInitialize = true;
				return;
			}
			else if(theConfig.NtFileExists(launchPath + "\\" + launchXbe)) 
			{
				theLauncher.children[0].children[0].theWholeThing.alpha = 0;
				UpdateRecentTitles(x,y);
				theLaunchGameLevel.GoTo();
				return;
			}
		}
	}
}

function GetTitleList(mnuName)
{
	CacheFile.section = "Cache";
	var TitleList = new Array;
	var TempList = new Array;
	var i = 1;
	TempList = Split(CacheFile.GetValue(mnuName),"|");
	while(TempList.length == 25)
	{
		TitleList = TitleList.concat(TempList);
		TempList = Split(CacheFile.GetValue(mnuName + "-" + i),"|");
		i = i + 1;
	}
	TitleList = TitleList.concat(TempList);
	return TitleList.sort();
}

DEF theLauncher Level
{
	archive "Music_PlayEdit2.xip"
	unloadable false
	children
	[
		Inline
		{
			url "Music_PlayEdit2/default2.xap"
			
			function onLoad()
			{
				
				var c = theLauncher.children[0].children[0];
				
				if( thcorb == true ) 
				{
					c.MU_L3_musicpod.visible = false;
					c.MU_L3_musicpod_thc.visible = true; 
					c.Spinning_Support.children[0].rpm = -1.3;
					c.Spinning_Support2.children[0].rpm = -0.8;
					c.Spinning_Support3.children[0].rpm = 1.4;
					c.Spinning_Support4.children[0].rpm = -1.6;
				}   
				else
				{ 
					c.MU_L3_musicpod.visible = true;
					c.MU_L3_musicpod_thc.visible = false; 
					c.Spinning_Support.children[0].rpm = 0;
					c.Spinning_Support2.children[0].rpm = 0;
					c.Spinning_Support3.children[0].rpm = 0;
					c.Spinning_Support4.children[0].rpm = 0;
				}
				
				EnterLauncherMenu();
			}
		}
	]
	
	shell Transform
	{
		scale 6.3176 6.3176 6.3176
		//scale 4.3176 4.3176 4.3176
		translation -47.220001 55 -204.300003
		children
		[
			DEF theLauncherWaver Waver
			{
				rpm 0.75
				children
				[
					Shape
					{
						appearance Appearance
						{
							material MaxMaterial
							{
								name "InnerWall_01"
							}
							texture ImageTexture
							{
								alpha true
								url "cellwall.bmp"
							}
						}
						geometry Mesh { url "Inner_cell-FACES.xm" }
					}
					Shape
					{
						appearance Appearance
						{
							material MaxMaterial
							{
								name "InnerWall_02"
							}
						}
						geometry Sphere
						{
							radius 1.0
						}
					}
				]
			}
		]
	}
	
	tunnel Transform
	{
		children
		[
			Shape
			{
				appearance Appearance { material MaxMaterial { name "Tubes" } }
				geometry Mesh { url "Music/Tunnel_02-FACES.xm" }
			}
		]
	}
	
	path USE LauncherViewpoint
	
	control DEF theLauncherJoystick Joystick
	{
		function OnADown()
		{
			theLauncher.children[0].children[0].MU_select_pod_HL.children[0].appearance.material.param = 1;
			
			PlaySoundA();
			
			if (bLauncherVisible0)
			{
				var mnuName = arrMenuNames[nCurLauncherItem0];
				if(mnuName == "")
				{
					return;
				}
				else
				{
					CurFillDir = arrMenuPaths[nCurLauncherItem0];
					subTitle0 = mnuName;
					TempTitleList = GetTitleList(mnuName);
					thePlayerPanelIn.Play();
					StartTitleList();
				}
			}
			else if(bInRecentTitles)
			{
				LaunchItem(arrRecentPaths[nPlayCursor0],TempTitleList[nPlayCursor0]);
			}
			else
			{
				LaunchItem(CurFillDir,TempTitleList[nPlayCursor0]);
			}
		}
		
		function OnXDown()
		{
			if(bInRecentTitles) return;
			PlaySoundA();
			subTitle1 = subTitle0;
			subTitle0 = Translate("Recent Titles");
			TempTitleList = arrRecentNames;
			if(bLauncherVisible0) 
			{
				thePlayerPanelIn.Play();
				StartTitleList(); 
			} 
			else 
			{
				nPlayScroll1 = nPlayScroll0;
				nPlayCursor1 = nPlayCursor0;
				nPlayScroll0 = 0;
				nPlayCursor0 = 0;
				bRecentToggled = true;
				UpdateTrackList(); 
			}
			bInRecentTitles = true;
		}
		
		function OnYDown()
		{
			
		}
		
		function OnBDown()
		{
			PlaySoundB();
			
			if (bLauncherVisible0)
			{
				theLauncher.children[0].children[0].MU_back_pod_HL.children[0].appearance.material.param = 1;
				if(bBackToSettings)
				{
					bBackToSettings = false;
					theSettingsSubMenuOut.Play();
					GoToSettings();
				}
				else
				{
					theGamesMenuOut.Play();
					theMainMenu.GoBackTo();
				}
			}
			else if(bRecentToggled)
			{
				TempTitleList = GetTitleList(arrMenuNames[nCurLauncherItem0]);
				nPlayScroll0 = nPlayScroll1;
				nPlayCursor0 = nPlayCursor1;
				subTitle0 = subTitle1;
				bInRecentTitles = false;
				bRecentToggled = false;
				UpdateTrackList();
			}
			else
			{
				theLauncher.children[0].children[0].MU_back_pod_HL.children[0].appearance.material.param = 1;
				CurrentViewpoint = LauncherViewpoint;
				ShowLauncherMenu();
				thePlayerPanelOut.Play();
			}
		}
		
		function OnMoveRight()  
		{ 
			if (bLauncherVisible0 == false)
			{
				var a = (TempTitleList.length - 1);
				if (nPlayCursor0 + nPlayVisible0 <= a)
				{
					nPlayCursor0 = nPlayCursor0 + nPlayVisible0;
					nPlayScroll0 = nPlayScroll0 + nPlayVisible0;
				}
				else if(nPlayCursor0 == a)
				{
					return;
				} 
				else if(a - nPlayCursor0 < nPlayVisible0)
				{
					nPlayCursor0 = a;
				}
				PlaySoundMenuChange();
				UpdateTrackList();
			}
		}
		
		function OnMoveLeft()  
		{  
			if (bLauncherVisible0 == false)
			{
				var a = (TempTitleList.length - 1);
				if (nPlayCursor0 - nPlayVisible0 >= 0)
				{
					nPlayCursor0 = nPlayCursor0 - nPlayVisible0;
					nPlayScroll0 = nPlayScroll0 - nPlayVisible0;
				}
				else if(nPlayCursor0 == 0)
				{
					return;
				} 
				else if(nPlayCursor0 - nPlayVisible0 < nPlayVisible0)
				{
					nPlayCursor0 = 0;
					nPlayScroll0 = 0;
				}
				PlaySoundMenuChange();
				UpdateTrackList();
			}
		}
		
		function OnMoveUp()
		{
			if (bLauncherVisible0)
			{
				if (nCurLauncherItem0 > 0)
				{
					PlaySoundMenuChange();
					nCurLauncherItem0 = nCurLauncherItem0 - 1;
					if(nCurLauncherItem0 < nCurLauncherScroll0) nCurLauncherScroll0 = nCurLauncherItem0; 
					UpdateLauncherMenu();
				}
			}
			else
			{
				var a = (TempTitleList.length - 1);
				if (nPlayCursor0 <= 0)
				{
					nPlayCursor0 = a;
					if(a > nPlayVisible0) nPlayScroll0 = a - nPlayVisible0;
				}
				else
				{
					nPlayCursor0 = nPlayCursor0 - 1;
				}
				PlaySoundMenuChange();
				UpdateTrackList();
			}
		}
		
		function OnMoveDown()
		{
			if (bLauncherVisible0)
			{
				if (nCurLauncherItem0 < TotalSections - 1)
				{
					PlaySoundMenuChange();
					nCurLauncherItem0 = nCurLauncherItem0 + 1; 
					if(nCurLauncherItem0 - nCurLauncherScroll0 >= nCurLauncherVisible0) nCurLauncherScroll0 = nCurLauncherScroll0 + 1; 
					UpdateLauncherMenu();
				}
			}
			else
			{
				var a = (TempTitleList.length - 1);
				if (nPlayCursor0 == a)
				{
					nPlayCursor0 = 0;
				}
				else
				{
					nPlayCursor0 = nPlayCursor0 + 1;
				}
				PlaySoundMenuChange();
				UpdateTrackList();
			}
		}
	}
	
	function OnActivate()
	{
		if (children[0].children.length() > 0)
		{
			EnterLauncherMenu();
		}
	}
}